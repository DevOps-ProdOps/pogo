#!/usr/local/bin/perl
use strict;
use warnings;
use Getopt::Std;
use AnyEvent::HTTPD;
use Log::Log4perl qw(:easy);
use FindBin qw( $Bin );
use Sysadm::Install qw( slurp );

my $PORT = 5000;

getopts "v", \my %opts;

my @l4p = ( level => $INFO, category => "main" );
if ( $opts{ v } ) {
    @l4p = ( level => $DEBUG );
}
Log::Log4perl->easy_init({ @l4p, layout => "%F{1}-%L> %m%n" });

my $httpd = AnyEvent::HTTPD->new ( port => $PORT );

INFO "Listening on port $PORT";

$httpd->reg_cb (
    file_mapper( "/pogo/assets" ),
    file_mapper( "/pogo/js" ),
    file_mapper( "", "$Bin/../ui/index.html" ),
);

$httpd->run;

###########################################
sub file_mapper {
###########################################
    my( $subdir, $fixed ) = @_;

    my $typemap = {
        ".js"  => "application/javascript",
        ".css" => "text/css",
        ".jpg" => "image/jpeg",
    };

    return $subdir, sub {
        my ($httpd, $req) = @_;

        my $path = $fixed;

        if( !$fixed ) {
            my $urlpath = $req->url()->path();
            DEBUG "URL path is $urlpath";

            $urlpath =~ s#^/pogo##;
            $path = "$Bin/../ui" . $urlpath;
        }

        DEBUG "File path is $path";

        if( -f $path ) {
            my $content_header = 'text/html';
            my( $suffix ) = ( $path =~ /(\.[^.]+)$/ );

            if( exists $typemap->{ $suffix } ) {
                $content_header = $typemap->{ $suffix };
            }

            DEBUG "Serving up with content header: $content_header";

            $req->respond ({ content => [ $content_header,
                    slurp "$path"
            ]});
        } else {
            DEBUG "Sending back 404";
            $req->respond ( [404, "Not Found" ] );
        }

        $httpd->stop_request();
    };
}

__END__

=head1 NAME

pogo-api - Pogo UI

=head1 SYNOPSIS

pogo-ui [options]

  Options:
    -v

=head1 DESCRIPTION

Start up the Pogo UI server.

=head1 LICENSE

Copyright (c) 2010-2012 Yahoo! Inc. All rights reserved.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
imitations under the License.

=head1 AUTHORS

Mike Schilli <m@perlmeister.com>
Ian Bettinger <ibettinger@yahoo.com>

Many thanks to the following folks for implementing the
original version of Pogo: 

Andrew Sloane <andy@a1k0n.net>, 
Michael Fischer <michael+pogo@dynamine.net>,
Nicholas Harteau <nrh@hep.cat>,
Nick Purvis <nep@noisetu.be>,
Robert Phan <robert.phan@gmail.com>,
Srini Singanallur <ssingan@yahoo.com>,
Yogesh Natarajan <yogesh_ny@yahoo.co.in>

